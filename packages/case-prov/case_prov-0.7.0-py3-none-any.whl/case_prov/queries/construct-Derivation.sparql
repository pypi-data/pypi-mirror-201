# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

CONSTRUCT
{
  # A lengthy explanation on why Generation and Usage are not made here
  # is inlined in case_prov_dot.py.
  # Also note that this query intentionally creates a Derivation node
  # per ?nObjectA binding.  A test done in prov-check raises a
  # PROV-CONSTRAINTs error (though, the test ID has not yet been
  # determined) if a Derivation houses two prov:entity values.
  ?nObjectB
    prov:qualifiedDerivation [
      a prov:Derivation ;
      prov:entity ?nObjectA ;
      prov:hadActivity ?nAction ;
    ] ;
    .
}
WHERE {
  ?nAction
    a case-investigation:InvestigativeAction ;
    uco-action:result ?nObjectB ;
    .

  # When an object is recorded as an action input, use that object.
  # When no object is recorded as an action input, instead use
  # prov:EmptyCollection.
  {
    SELECT ?nAction ?nObjectA
    WHERE {
      ?nAction
        uco-action:object ?nObjectA ;
        .
    }
  }
  UNION
  {
    SELECT ?nAction ?nObjectA
    WHERE {
      BIND ( prov:EmptyCollection AS ?nObjectA )
      FILTER NOT EXISTS {
        ?nAction
          uco-action:object ?nObject ;
          .
      }
    }
  }
}
