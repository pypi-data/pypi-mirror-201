# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

# Extended documentation of the WHERE clause in this query is in
# construct-wasInformedBy.sparql.

CONSTRUCT
{
  ?nUsingAction
    prov:qualifiedCommunication ?nCommunication ;
    .
  ?nCommunication
    a prov:Communication ;
    prov:activity ?nMostRecentAction ;
    .
}
WHERE {
  ?nMostRecentProvenanceRecord a case-investigation:ProvenanceRecord .

  {
    ?nMostRecentAction uco-action:result ?nMostRecentProvenanceRecord .
    ?nUsingAction uco-action:object ?nMostRecentProvenanceRecord .

    # Create only one prov:Communication blank node per pair of
    # nUsingAction--nMostRecentAction values, even if multiple ?nResult
    # solutions are found.
    # https://stackoverflow.com/a/46155992
    BIND(BNODE() AS ?nCommunication)
  }

  ?nUsingAction uco-action:object ?nResult .
  ?nMostRecentProvenanceRecord uco-core:object ?nResult .
}
